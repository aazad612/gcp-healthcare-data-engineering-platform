locals {
  # ENV → workload projects it owns
  cb_to_projects = {
    np = ["clin_syn_np"]  # NP Cloud Build should access NP synthea project only
    pd = ["clin_syn_pd"]  # PD Cloud Build should access PD synthea project only
  }
}

data "google_bigquery_datasets" "synthea" {
  for_each = toset(flatten(values(local.cb_to_projects)))
  project  = local.project_ids[each.key]
}

data "google_project" "target" {
  for_each   = var.target_projects   # np → shared_orch_np, pd → shared_orch_pd
  project_id = local.project_ids[each.value]
}

locals {
  cloudbuild_sas = {
    for env, proj_key in var.target_projects :
    env => "${data.google_project.target[env].number}@cloudbuild.gserviceaccount.com"
  }
}

locals {
  # Flatten structure:
  # np ⇒ [{env="np", proj="clin_syn_np", dataset="bronze"}, ...]
  cb_synthea_bindings = flatten([
    for env, proj_list in local.cb_to_projects : [
      for proj_key in proj_list : [
        for dataset in data.google_bigquery_datasets.synthea[proj_key].datasets : {
          env        = env
          dataset_id = dataset.dataset_id
          project_id = local.project_ids[proj_key]
        }
      ]
    ]
  ])
}


resource "google_bigquery_dataset_access" "cb_editor_on_synthea" {
  for_each = {
    for b in local.cb_synthea_bindings :
    "${b.env}_${b.project_id}_${b.dataset_id}" => b
  }

  dataset_id    = each.value.dataset_id
  project       = each.value.project_id
  role          = "roles/bigquery.dataEditor"
  user_by_email = local.cloudbuild_sas[each.value.env]
}
